;*******************************************************************************
;
; Title:		display_local_fields.dbl
;
; Description:	Displays information to local display fields by calling the
;				change methods for any field in an input set.  The change
;				methods need to support this functionality.  Refer to the
;				CodeGen documentation for more information.
;
; Author:		Richard Morris
;
; Copyright:    ©Synergex International Inc.  All rights reserved.
;
; WARNING:      If you were given this code by a Synergex employee then you may
;               use and modify it freely for use with your own applications.
;               However, you may not under any circumstances distribute this
;               code, or any modified version of this code, to any third party
;               without first obtaining written permission to do so from
;               Synergex.  In using this code you accept that it is provided
;               as is, and without support or warranty.  Neither Synergex or the
;               author accept any responsibility for any losses or damages of
;               any nature which may arise from the use of this code.  This
;               header information must remain, unaltered, in the code at all
;               times.  Posession of this code, or any modified version of this
;               code, indicates your acceptance of these terms.
;
;*******************************************************************************

subroutine display_local_fields, REENTRANT

	;Argument list
	a_wndid			,n	;Window id
	a_data			,a	;Window data area

	;Method data
	methdat1		,a
	methdat2		,a
	methdat3		,a
	methdat4		,a
	methdat5		,a
	methdat6		,a
	methdat7		,a
	methdat8		,a
	methdat9		,a
	methdat10		,a
	methdat11		,a
	methdat12		,a
	methdat13		,a
	methdat14		,a
	methdat15		,a
	methdat16		,a
	methdat17		,a
	methdat18		,a
	methdat19		,a
	methdat20		,a

	endparams

	.include "WND:system.def"
	.include "WND:tools.def"
	.include "WND:setinf.def"
	.include "WND:fldinf.def"

	stack record
	.include 'WND:inpinf.def'
	endrecord
	
	stack record
		count			,i4		;Field counter
		pendsts			,i4		;Pending status for change methods
		set_number		,i4		;Ordinal set number
		fld_number		,i4		;Field number
		save_data		,i4		;Dynamic memory handle
		fld_name		,a30	;Name of input field
	endrecord
	
	global data section DISPLAY_LOCAL_FIELDS_DATA ,init
		record
			dlf_dispfld	,a30	;Used to propagate current field name between this
								;routine and generated change methods
		endrecord
	endglobal

proc

	clear set_number, ^i(inputinfo)

	;Save original input data area
	save_data = %mem_proc(DM_ALLOC, ^size(a_data))
	^m(save_data) = a_data

	;Get set info for the ALL set
	xcall i_setinf(a_wndid, "ALL", set_number,, gs_fldset)

	for count from 1 thru gs_setcnt
	begin

		;Get the field name
		clear fld_name
		xcall i_fldinf(a_wndid, fld_name, gs_setfldi(count), , gs_inpfld)

		;If the field has a change method, call it
		if (gs_change && (gs_change!='C_NOCHANGE'))
		begin

			;Temporarily turn off the change method
			xcall i_fldmod(a_wndid, fld_name, , , D_OFF, D_FLD_CHANGE)

			;Simulate an inputinfo record
			inp_wndid = a_wndid
			inp_setnum = -99

			;Field name used by change methods when NOT called by I_INPUT
			dlf_dispfld = fld_name

			;Call the change method
			onerror no_routine
			xcall xsubr(gs_change,
				&			a_data(gs_pos:gs_siz),			;Data entered
				&			a_data(gs_pos:gs_siz),			;Data stored
				&			pendsts = D_OK,					;Pending status
				&			inputinfo,						;Input info block
				&			^m(save_data),					;Input record
				&			methdat1, methdat2, methdat3, methdat4, methdat5,
				&			methdat6, methdat7, methdat8, methdat9, methdat10,
				&			methdat11, methdat12, methdat13, methdat14, methdat15,
				&			methdat16, methdat17, methdat18, methdat19, methdat20)

			if (false)
no_routine,		offerror

			;Turn the change method back on
			xcall i_fldmod(a_wndid, fld_name, , , D_FLD_CHANGE, gs_change)
			offerror

		end
	end

	xcall i_display(a_wndid, "ALL", ^m(save_data))
	save_data = %mem_proc(DM_FREE,save_data)

	clear dlf_dispfld

	xreturn

endsubroutine


