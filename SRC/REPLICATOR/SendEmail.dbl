;;*****************************************************************************
;;

import replicator

subroutine SendEmail
    required in subject, string
    optional in body, [#]string
    optional in bodyhtml, boolean
    optional in recipientEmail, string

    .include "REPLICATOR_INCLUDE:SmtpMail.def"

    stack record
        emailStatus, i4
        recipientAddress, string
        bodyData, [#]string
        html, boolean
    endrecord
proc
    emailStatus = 0
    html = ^passed(bodyhtml) && bodyhtml

    if (^passed(body)) then
    begin
        bodyData = body
    end
    else
    begin
        bodyData = new string[#] { "" }
    end

    if (^passed(recipientEmail) && recipientEmail.Length>0) then
    begin
        recipientAddress = recipientEmail
    end
    else
    begin
        recipientAddress = Settings.ErrorEmail
    end

    if ((emailStatus=%SmtpMail(Settings.EmailServer,Settings.EmailSender,"SQL Replicator",,recipientAddress,,subject,bodyData,,html)) != SMERR_SUCCESS)
    begin
        Logger.ErrorLog("Failed to send error email. Error was " + %atrim(SmtpErrorText[emailStatus]))
    end

    xreturn

endsubroutine

