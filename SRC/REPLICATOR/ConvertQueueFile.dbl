
;;; <summary>
;;; This program can be used to convert the transaction queue file to the new format introduced in July 2018.
;;; The change is to switch from the previously used A20 primary key populated with a %DATETIME value to an
;;; I8 "auto date" key. The reason for the change it to avoid the expensive "retry" logic that would execute
;;; when performing a large numbers of operations in a short time, resulting from the poor granularity of the
;;; data from %DATETIME. This program will create REPLICATOR_DATA:REPLICATION_NEW.ISM and you should then rename
;;; away your current REPLICATION.ISM file and replace it with the new one.
;;; </summary>

main ConvertQueueFile

.ifdef OS_VMS
	.include "REPLICATION_VMS" repository, record="replication"
	.include "REPLICATION_VMS_OLD" repository, record="replication_old"
.else
	.include "REPLICATION" repository, record="replication"
	.include "REPLICATION_OLD" repository, record="replication_old"
.endc

	record
		chin,	int
		chout,	int
	endrecord

proc

	open(chin=0, i:i,"REPLICATOR_DATA:REPLICATION.ISM")
	open(chout=0,o:i,"REPLICATOR_DATA:REPLICATION_NEW.ISM",FDL:"@REPLICATOR_XDL:REPLICATION.XDL")

	repeat
	begin
		;;Get the next record to convert
		reads(chin,replication_old,eof)

		;;The primary key is now auto allocated
		init replication.transaction_id

		;;Retain the remaining data
		replication.action = replication_old.action
		replication.structure_name = replication_old.structure_name
		replication.record = replication_old.record

		;;And save the new record
		store(chout,replication)
	end

eof,
	close chout
	close chin

	stop

endmain